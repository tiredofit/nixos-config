{
  description = "Tired of I.T! NixOS Configuration";

  nixConfig = {
    experimental-features = [ "nix-command" "flakes" ];
    extra-substituters = [
      "https://nix-community.cachix.org"
      "https://nix-gaming.cachix.org"
      "https://hyprland.cachix.org"
    ];
    extra-trusted-public-keys = [
      "cache.nixos.org-1:6NCHdD59X431o0gWypbMrAURkbJ16ZPMQFGspcDShjY="
      "hyprland.cachix.org-1:a7pgxzMz7+chwVL3/pzj6jIBMioiJM7ypFP8PwtkuGc="
      "nix-community.cachix.org-1:mB9FSh9qf2dCimDSUo8Zy7bkq5CX+/rkCWyvRCYg3Fs="
      "nix-gaming.cachix.org-1:nbjlureqMbRAxR1gJ/f3hxemL9svXaZF/Ees8vCUUs4="
    ];
  };

  inputs = {
    nixpkgs.url = "github:NixOS/nixpkgs/nixos-unstable";
    disko = {
      url = "github:nix-community/disko";
      inputs.nixpkgs.follows = "nixpkgs";
    };
    hardware.url = "github:nixos/nixos-hardware";
    home-manager = {
      url = "github:nix-community/home-manager";
      inputs.nixpkgs.follows = "nixpkgs";
    };
    hyprland.url = "github:hyprwm/Hyprland";
    hyprwm-contrib = {
      url = "github:hyprwm/contrib";
      inputs.nixpkgs.follows = "nixpkgs";
    };
    impermanence.url = "github:nix-community/impermanence";
    nix-gaming = {
      url = "github:fufexan/nix-gaming";
      inputs.nixpkgs.follows = "nixpkgs";
    };
    nur.url = "github:nix-community/NUR";
    sops-nix = {
      url = "github:Mic92/sops-nix";
      inputs.nixpkgs.follows = "nixpkgs";
    };
    vscode-server.url = "github:nix-community/nixos-vscode-server";
  };

  outputs = { self, nixpkgs, ... }@inputs:
    let
      inherit (self) outputs;
      lib = nixpkgs.lib;
      systems = [ "x86_64-linux" "aarch64-linux" ];
      forEachSystem = f: lib.genAttrs systems (sys: f pkgsFor.${sys});
      pkgsFor = lib.genAttrs systems (system: import nixpkgs {
        inherit system;
        config.allowUnfree = true;
      });
    in
    {
      inherit lib;
      nixosModules = import ./modules;
      overlays = import ./overlays { inherit inputs outputs; };
      packages = forEachSystem (pkgs: import ./pkgs { inherit pkgs; });
      devShells = forEachSystem (pkgs: import ./shell.nix { inherit pkgs; });
      formatter = forEachSystem (pkgs: pkgs.nixpkgs-fmt);

      nixosConfigurations = {

        beef = lib.nixosSystem { # Workstation
          modules = [ ./hosts/beef ];
          specialArgs = { inherit inputs outputs; };
        };

        beer = lib.nixosSystem { # Bar
          modules = [ ./hosts/beer ];
          specialArgs = {
            inherit inputs outputs;
            kioskUsername = "dave";
            kioskURL = "https://beer.tiredofit.ca";
          };
        };

        butcher = lib.nixosSystem { # Local Server
          modules = [ ./hosts/butcher ];
          specialArgs = { inherit inputs outputs; };
        };

        disko = lib.nixosSystem { # Disko
          modules = [ ./hosts/disko ];
          specialArgs = { inherit inputs outputs; };
          system = "x86_64-linux";
        };

        nakulaptop = lib.nixosSystem { # Laptop
          modules = [ ./hosts/nakulaptop ];
          specialArgs = { inherit inputs outputs; };
        };

        selecta = lib.nixosSystem { # Production Station
          modules = [ ./hosts/selecta ];
          specialArgs = { inherit inputs outputs; };
        };

        soy = lib.nixosSystem { # VM
          modules = [ ./hosts/soy ];
          specialArgs = { inherit inputs outputs; };
        };
      };
    };
}
